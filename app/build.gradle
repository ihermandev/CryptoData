plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

apply from: "$rootDir/gradle/versions.gradle"

android {

    compileSdkVersion androidCompileSdk

    defaultConfig {
        applicationId "i.herman.cryptodata"
        minSdkVersion androidMinSdk
        targetSdkVersion androidTargetSdk
        versionCode versionCode
        versionName versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "BASE_API_URL", '"https://gist.githubusercontent.com/izzi888/fa2efec7a9882238e7511a7020581bee/"')

            resValue "string", "app_name", "CryptoData"
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField("String", "BASE_API_URL", '"https://gist.githubusercontent.com/izzi888/fa2efec7a9882238e7511a7020581bee/"')

            resValue "string", "app_name", "CryptoData Debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        animationsDisabled = true
    }
}

dependencies {
    //AndroidX libs
    implementation "androidx.core:core-ktx:$coreKtxVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVesrion"
    implementation "com.google.android.material:material:$materialVesrion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentKtxVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleLivedata"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleViewmodel"
    implementation "androidx.legacy:legacy-support-v4:$legacyVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerviewVersion"

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // DI Dagger Hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hiltLiecycleVersion"

    //My lib with frequently used extensions
    implementation "com.github.izzi888:UsefulKotlinUtil:$izzi888Version"

    //Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.github.bumptech.glide:annotations:$glideVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    //Room
    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    //Log
    implementation "com.jakewharton.timber:timber:$timberVersion"

    //Testing
    implementation "com.jakewharton.espresso:okhttp3-idling-resource:$okhttp3IdlingVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoInlineVersion"
    testImplementation "androidx.arch.core:core-testing:$androixCoreTestingVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesTestVersion"

    androidTestImplementation "com.jakewharton.espresso:okhttp3-idling-resource:$okhttp3IdlingVersion"
    androidTestImplementation "androidx.test:runner:$androidxTestRunnerVesrion"
    androidTestImplementation "androidx.test.ext:junit:$androidxTestJunitVesrion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoVersion"
    androidTestImplementation("com.adevinta.android:barista:$baristaVersion") {
        exclude group: 'org.jetbrains.kotlin'
    }
}
